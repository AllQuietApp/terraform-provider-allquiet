---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "allquiet_team_escalations Resource - allquiet"
subcategory: ""
description: |-
  The Team Escalations resource represents a Team's Escalation Tiers in All Quiet. Escalation Tiers are used to group members and define schedules, its tiers and rorations.
---

# allquiet_team_escalations (Resource)

The Team Escalations resource represents a Team's Escalation Tiers in All Quiet. Escalation Tiers are used to group members and define schedules, its tiers and rorations.

## Example Usage

```terraform
resource "allquiet_user" "riemann" {
  display_name = "Riemann"
  email        = "acceptance-tests+riemann@allquiet.app"
}

resource "allquiet_user" "galois" {
  display_name = "Galois"
  email        = "acceptance-tests+galois@allquiet.app"
}

resource "allquiet_user" "gauss" {
  display_name = "Gauss"
  email        = "acceptance-tests+gauss@allquiet.app"
}

resource "allquiet_user" "kolmogorov" {
  display_name = "Kolmogorov"
  email        = "acceptance-tests+kolmogorov@allquiet.app"
}

resource "allquiet_team" "my_team_with_weekend_rotation" {
  display_name = "My team with weekend rotation"
  time_zone_id = "America/Los_Angeles"
}

resource "allquiet_team_membership" "my_team_with_weekend_rotation_riemann" {
  team_id = allquiet_team.my_team_with_weekend_rotation.id
  user_id = allquiet_user.riemann.id
  role    = "Administrator"
}

resource "allquiet_team_membership" "my_team_with_weekend_rotation_galois" {
  team_id = allquiet_team.my_team_with_weekend_rotation.id
  user_id = allquiet_user.galois.id
  role    = "Member"
}

resource "allquiet_team_membership" "my_team_with_weekend_rotation_gauss" {
  team_id = allquiet_team.my_team_with_weekend_rotation.id
  user_id = allquiet_user.gauss.id
  role    = "Member"
}

resource "allquiet_team_membership" "my_team_with_weekend_rotation_kolmogorov" {
  team_id = allquiet_team.my_team_with_weekend_rotation.id
  user_id = allquiet_user.kolmogorov.id
  role    = "Member"
}

resource "allquiet_team_escalations" "my_team_escalations_with_weekend_rotation" {
  team_id = allquiet_team.my_team_with_weekend_rotation.id
  escalation_tiers = [
    {
      auto_escalation_after_minutes = 5
      schedules = [
        {
          schedule_settings = {
            selected_days = ["mon", "tue", "wed", "thu", "fri"]
          }
          rotations = [
            {
              members = [
                {
                  team_membership_id = allquiet_team_membership.my_team_with_weekend_rotation_riemann.id
                },
                {
                  team_membership_id = allquiet_team_membership.my_team_with_weekend_rotation_galois.id
                },
                {
                  team_membership_id = allquiet_team_membership.my_team_with_weekend_rotation_gauss.id
                },
                {
                  team_membership_id = allquiet_team_membership.my_team_with_weekend_rotation_kolmogorov.id
                }
              ]
            }
          ]
        },

        {
          schedule_settings = {
            selected_days = ["sat", "sun"]
          }
          rotation_settings = {
            repeats               = "weekly"
            starts_on_day_of_week = "sat"
          }
          rotations = [
            {
              members = [
                {
                  team_membership_id = allquiet_team_membership.my_team_with_weekend_rotation_riemann.id
                },
                {
                  team_membership_id = allquiet_team_membership.my_team_with_weekend_rotation_galois.id
                }
              ]
            },
            {
              members = [
                {
                  team_membership_id = allquiet_team_membership.my_team_with_weekend_rotation_gauss.id
                },
                {
                  team_membership_id = allquiet_team_membership.my_team_with_weekend_rotation_kolmogorov.id
                }
              ]
            }
          ]
        }
      ]
    }
  ]
}

resource "allquiet_team" "my_team_with_day_and_night_rotation" {
  display_name = "My team with day and night rotation"
  time_zone_id = "America/Los_Angeles"
}


resource "allquiet_team_membership" "my_team_with_day_and_night_rotation_riemann" {
  team_id = allquiet_team.my_team_with_day_and_night_rotation.id
  user_id = allquiet_user.riemann.id
  role    = "Administrator"
}

resource "allquiet_team_membership" "my_team_with_day_and_night_rotation_galois" {
  team_id = allquiet_team.my_team_with_day_and_night_rotation.id
  user_id = allquiet_user.galois.id
  role    = "Member"
}

resource "allquiet_team_membership" "my_team_with_day_and_night_rotation_gauss" {
  team_id = allquiet_team.my_team_with_day_and_night_rotation.id
  user_id = allquiet_user.gauss.id
  role    = "Member"
}

resource "allquiet_team_membership" "my_team_with_day_and_night_rotation_kolmogorov" {
  team_id = allquiet_team.my_team_with_day_and_night_rotation.id
  user_id = allquiet_user.kolmogorov.id
  role    = "Member"
}

resource "allquiet_team_escalations" "my_team_escalations_with_day_and_night_rotation" {
  team_id = allquiet_team.my_team_with_day_and_night_rotation.id
  escalation_tiers = [
    {
      auto_escalation_after_minutes = 5
      schedules = [
        {
          schedule_settings = {
            start : "08:00"
            end : "20:00"
          }
          rotations = [
            {
              members = [
                {
                  team_membership_id = allquiet_team_membership.my_team_with_day_and_night_rotation_riemann.id
                },
                {
                  team_membership_id = allquiet_team_membership.my_team_with_day_and_night_rotation_galois.id
                },
                {
                  team_membership_id = allquiet_team_membership.my_team_with_day_and_night_rotation_gauss.id
                },
                {
                  team_membership_id = allquiet_team_membership.my_team_with_day_and_night_rotation_kolmogorov.id
                }
              ]
            }
          ]
        },
        {
          schedule_settings = {
            start : "20:00"
            end : "08:00"
          }
          rotation_settings = {
            repeats               = "weekly"
            starts_on_day_of_week = "sat"
          }
          rotations = [
            {
              members = [
                {
                  team_membership_id = allquiet_team_membership.my_team_with_day_and_night_rotation_riemann.id
                },
                {
                  team_membership_id = allquiet_team_membership.my_team_with_day_and_night_rotation_galois.id
                }
              ]
            },
            {
              members = [
                {
                  team_membership_id = allquiet_team_membership.my_team_with_day_and_night_rotation_gauss.id
                },
                {
                  team_membership_id = allquiet_team_membership.my_team_with_day_and_night_rotation_kolmogorov.id
                }
              ]
            }
          ]
        }
      ]
    }
  ]

}

resource "allquiet_team" "my_team_with_hourly_rotation" {
  display_name = "My team with hourly rotation"
  time_zone_id = "America/Los_Angeles"
}


resource "allquiet_team_membership" "my_team_with_hourly_rotation_riemann" {
  team_id = allquiet_team.my_team_with_hourly_rotation.id
  user_id = allquiet_user.riemann.id
  role    = "Administrator"
}

resource "allquiet_team_membership" "my_team_with_hourly_rotation_galois" {
  team_id = allquiet_team.my_team_with_hourly_rotation.id
  user_id = allquiet_user.galois.id
  role    = "Member"
}

resource "allquiet_team_escalations" "my_team_escalations_with_hourly_rotation" {
  team_id = allquiet_team.my_team_with_hourly_rotation.id
  escalation_tiers = [
    {
      auto_escalation_after_minutes = 5
      schedules = [
        {
          rotation_settings = {
            repeats             = "custom"
            custom_repeat_unit  = "hours"
            custom_repeat_value = 6
            starts_on_time      = "00:00"
            effective_from      = "2024-07-10"
          }
          rotations = [
            {
              members = [
                {
                  team_membership_id = allquiet_team_membership.my_team_with_hourly_rotation_riemann.id
                },
                {
                  team_membership_id = allquiet_team_membership.my_team_with_hourly_rotation_galois.id
                },
              ]
            }
          ]
        },
      ]
    }
  ]
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `team_id` (String) Id of the associated team

### Optional

- `escalation_tiers` (Attributes List) (see [below for nested schema](#nestedatt--escalation_tiers))

### Read-Only

- `id` (String) Id

<a id="nestedatt--escalation_tiers"></a>
### Nested Schema for `escalation_tiers`

Required:

- `schedules` (Attributes List) (see [below for nested schema](#nestedatt--escalation_tiers--schedules))

Optional:

- `auto_escalation_after_minutes` (Number) After how many minutes the incident should be escalated to the next tier.

<a id="nestedatt--escalation_tiers--schedules"></a>
### Nested Schema for `escalation_tiers.schedules`

Required:

- `rotations` (Attributes List) (see [below for nested schema](#nestedatt--escalation_tiers--schedules--rotations))

Optional:

- `rotation_settings` (Attributes) Settings for the rotation (see [below for nested schema](#nestedatt--escalation_tiers--schedules--rotation_settings))
- `schedule_settings` (Attributes) Settings for the schedule (see [below for nested schema](#nestedatt--escalation_tiers--schedules--schedule_settings))

<a id="nestedatt--escalation_tiers--schedules--rotations"></a>
### Nested Schema for `escalation_tiers.schedules.rotations`

Required:

- `members` (Attributes List) (see [below for nested schema](#nestedatt--escalation_tiers--schedules--rotations--members))

<a id="nestedatt--escalation_tiers--schedules--rotations--members"></a>
### Nested Schema for `escalation_tiers.schedules.rotations.members`

Required:

- `team_membership_id` (String) Id of the team membership



<a id="nestedatt--escalation_tiers--schedules--rotation_settings"></a>
### Nested Schema for `escalation_tiers.schedules.rotation_settings`

Required:

- `repeats` (String) The rotation will repeat on the given interval

Optional:

- `custom_repeat_unit` (String) In what interval unit the rotation should repeat. Needs to be set if 'repeats' is 'custom'
- `custom_repeat_value` (Number) How often the rotation should repeat. Needs to be set if 'repeats' is 'custom'
- `effective_from` (String) If sets, the rotation will be effective from the given date in ISO 8601 format
- `starts_on_date_of_month` (Number) If set, starts on date of the month. Needs to be set if 'repeats' is 'monthly'
- `starts_on_day_of_week` (String) Starts on day of the week. Needs to be set if 'repeats' is not 'monthly'. Possible values are: sun, mon, tue, wed, thu, fri, sat
- `starts_on_time` (String) If set, starts on time of day. Needs to be set if 'repeats' is 'custom' and 'custom_repeat_unit' is 'hours'. Format: HH:mm


<a id="nestedatt--escalation_tiers--schedules--schedule_settings"></a>
### Nested Schema for `escalation_tiers.schedules.schedule_settings`

Optional:

- `end` (String) End time of the schedule. Format: HH:mm
- `selected_days` (List of String) Selected days of the week. Possible values are: sun, mon, tue, wed, thu, fri, sat
- `start` (String) Start time of the schedule. Format: HH:mm
